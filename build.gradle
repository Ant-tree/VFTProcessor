plugins { id 'java-library' }

group = 'com.anttree'
version = '1.0'

repositories {
    mavenCentral()
}

java { toolchain { languageVersion = JavaLanguageVersion.of(11) } }

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

// vft-processor-annotations.jar - annotations package only
tasks.register('annotationsJar', Jar) {
    archiveBaseName = "vft-processor-ann"
    archiveClassifier = ""
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from sourceSets.main.output
    include 'com/anttree/vft/processors/annotations/**'

    manifest {
        attributes(
                'Manifest-Version': '1.0'
        )
    }
}

// vft-processor.jar - rest of the packages except annotations
tasks.register('processorJar', Jar) {
    archiveBaseName = "vft-processor"
    archiveClassifier = ""
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes(
                'Manifest-Version': '1.0',
                'Main-Class': 'com.anttree.vft.processors.VftRunner'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from sourceSets.main.output
    exclude 'com/anttree/vft/processors/annotations/**'
}

jar {
    dependsOn annotationsJar, processorJar
    enabled = false
}
